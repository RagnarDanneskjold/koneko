; --                                                            ; {{{1
;
; File        : prelude.knk
; Maintainer  : Felix C. Stegerman <flx@obfusk.net>
; Date        : 2019-10-07
;
; Copyright   : Copyright (C) 2019  Felix C. Stegerman
; Version     : v0.0.1
; License     : LGPLv3+
;
; --                                                            ; }}}1

; -- TODO --
;
; * add more functions!
; * use multi for arithmetic
;
; --

; -- stack shuffling --

; --                                                            ; {{{1
;
; >>> , 1 2 show-stack
; 2
; 1
; >>> , swap show-stack
; 1
; 2
;
; >>> clear-stack
; >>> 42
; 42
; >>> , dup show-stack
; 42
; 42
;
; >>> clear-stack
; >>> nil
; nil
; >>> drop
; >>> drop
; *** ERROR: stack underflow
;
; >>> , 1 2 'dup dip swap show-stack
; 1
; 2
; 1
;
; >>> clear-stack
; >>> , 1 2 over show-stack
; 1
; 2
; 1
;
; --                                                            ; }}}1

:swap   [ x y . 'y 'x     ] def ; primitive
:dup    [ x   . 'x 'x     ] def
:drop   [ _   .           ] def
:dip    [ x f .  f 'x     ] def
:over   [ x y . 'x 'y 'x  ] def ; [ 'dup dip swap ]

; ... TODO ...

; -- arithmetic --

; --                                                            ; {{{1
;
; >>> 1 2 +
; 3
; >>> 4 3 -
; 1
; >>> 6 7 *
; 42
; >>> 1.0 2.0 /
; 0.5
; >>> 8 3 div
; 2
; >>> 8 3 mod
; 2
;
; --                                                            ; }}}1

; :+ ( :int   :int    ) [ __int+__          ] defmulti
; :+ ( :float :float  ) [ __float+__        ] defmulti
; :+ ( :int   :float  ) [ 'int->float dip + ] defmulti
; :+ ( :float :int    ) [  int->float     + ] defmulti

; :- ( :int   :int    ) [ __int-__          ] defmulti
; :- ( :float :float  ) [ __float-__        ] defmulti
; :- ( :int   :float  ) [ 'int->float dip - ] defmulti
; :- ( :float :int    ) [  int->float     - ] defmulti

; :* ( :int   :int    ) [ __int*__          ] defmulti
; :* ( :float :float  ) [ __float*__        ] defmulti
; :* ( :int   :float  ) [ 'int->float dip * ] defmulti
; :* ( :float :int    ) [  int->float     * ] defmulti

:+      [ __int+__    ] def
:-      [ __int-__    ] def
:*      [ __int*__    ] def

:/      [ __float/__  ] def
:div    [ __div__     ] def
:mod    [ __mod__     ] def

; -- sequences --

; --                                                            ; {{{1
;
; >>> () empty?
; #t
; >>> ( 1 2 ) empty?
; #f
; >>> ( :x :y :z ) len
; 3
;
; >>> ( :one :two :three ) 1 get
; :two
; >>> () 0 get
; *** ERROR: list.get: index error
;
; --                                                            ; }}}1

:empty? [ .empty?   ] def
:len    [ .len      ] def
:get    [ swap !get ] def ; TODO: safe version

; -- lists --

; --                                                            ; {{{1
;
; >>> ( 1 2 3 )
; ( 1 2 3 )
; >>> dup head
; 1
; >>> drop dup tail
; ( 2 3 )
;
; >>> , drop uncons show-stack
; ( 2 3 )
; 1
; >>> cons
; ( 1 2 3 )
;
; >>> () head
; *** ERROR: list.head: empty list
; >>> () tail
; *** ERROR: list.tail: empty list
;
; >>> () head^
; nil
; >>> () tail^
; nil
;
; >>> ( 2 3 4 ) [ dup * ] map
; ( 4 9 16 )
; >>> ( 2 3 4 ) 10 '- foldl
; 1
;
; >>> , ( "Hello" "World" ) 'say each
; Hello
; World
;
; --                                                            ; }}}1

:head   [ .head   ] def
:tail   [ .tail   ] def

:head^  [ 'head 'empty? ornil ] def
:tail^  [ 'tail 'empty? ornil ] def

:uncons [ .uncons ] def
:cons   [ !cons   ] def

:map    [ f . dup empty? [ ] [ uncons 'f dip 'f map cons ] if ] def
:foldl  [ z f . dup empty? [ drop 'z ]
                [ uncons swap 'z swap f 'f foldl ] if ] def

:each   [ f . dup empty? [ drop ] [ uncons 'f dip 'f each ] if ] def

; ... TODO ...

; -- miscellaneous --

; --                                                            ; {{{1
;
; >>> , [ "Hi!" say ] 5 times
; Hi!
; Hi!
; Hi!
; Hi!
; Hi!
;
; --                                                            ; }}}1

:ornil  [ nil ordef ] def
:ordef  [ f p? d . dup p? [ drop 'd ] 'f if ] def

:times  [ f n . 'n 0 <= [ ] [ f 'f 'n 1 - times ] if ] def

; -- predicates --

; --                                                            ; {{{1
;
; >>> 1 num?
; #t
; >>> 3.14 num?
; #t
; >>> () num?
; #f
;
; --                                                            ; }}}1

:num?   [ dup int? 'float? dip or ] def

; -- TODO --

; ...

; vim: set tw=70 sw=2 sts=2 et fdm=marker :
